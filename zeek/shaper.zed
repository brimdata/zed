// This reference Zed shaper for Zeek NDJSON logs was most recently tested with
// Zeek v4.1.0. The fields and data types reflect the default NDJSON
// logs output by that Zeek version when using the JSON Streaming Logs package.

type port=uint16;
type zenum=string;
type conn_id={orig_h:ip,orig_p:port,resp_h:ip,resp_p:port};

// This first block of type definitions cover the fields we've observed in
// an out-of-the-box Zeek v4.0.3 and earlier.

type broker={_path:string,ts:time,ty:zenum,ev:bstring,peer:{address:bstring,bound_port:port},message:bstring,_write_ts:time};
type capture_loss={_path:string,ts:time,ts_delta:duration,peer:bstring,gaps:uint64,acks:uint64,percent_lost:float64,_write_ts:time};
type cluster={_path:string,ts:time,node:bstring,message:bstring,_write_ts:time};
type config={_path:string,ts:time,id:bstring,old_value:bstring,new_value:bstring,location:bstring,_write_ts:time};
type conn={_path:string,ts:time,uid:bstring,id:conn_id,proto:zenum,service:bstring,duration:duration,orig_bytes:uint64,resp_bytes:uint64,conn_state:bstring,local_orig:bool,local_resp:bool,missed_bytes:uint64,history:bstring,orig_pkts:uint64,orig_ip_bytes:uint64,resp_pkts:uint64,resp_ip_bytes:uint64,tunnel_parents:|[bstring]|,_write_ts:time};
type dce_rpc={_path:string,ts:time,uid:bstring,id:conn_id,rtt:duration,named_pipe:bstring,endpoint:bstring,operation:bstring,_write_ts:time};
type dhcp={_path:string,ts:time,uids:|[bstring]|,client_addr:ip,server_addr:ip,mac:bstring,host_name:bstring,client_fqdn:bstring,domain:bstring,requested_addr:ip,assigned_addr:ip,lease_time:duration,client_message:bstring,server_message:bstring,msg_types:[bstring],duration:duration,_write_ts:time};
type dnp3={_path:string,ts:time,uid:bstring,id:conn_id,fc_request:bstring,fc_reply:bstring,iin:uint64,_write_ts:time};
type dns={_path:string,ts:time,uid:bstring,id:conn_id,proto:zenum,trans_id:uint64,rtt:duration,query:bstring,qclass:uint64,qclass_name:bstring,qtype:uint64,qtype_name:bstring,rcode:uint64,rcode_name:bstring,AA:bool,TC:bool,RD:bool,RA:bool,Z:uint64,answers:[bstring],TTLs:[duration],rejected:bool,_write_ts:time};
type dpd={_path:string,ts:time,uid:bstring,id:conn_id,proto:zenum,analyzer:bstring,failure_reason:bstring,_write_ts:time};
type files={_path:string,ts:time,fuid:bstring,tx_hosts:|[ip]|,rx_hosts:|[ip]|,conn_uids:|[bstring]|,source:bstring,depth:uint64,analyzers:|[bstring]|,mime_type:bstring,filename:bstring,duration:duration,local_orig:bool,is_orig:bool,seen_bytes:uint64,total_bytes:uint64,missing_bytes:uint64,overflow_bytes:uint64,timedout:bool,parent_fuid:bstring,md5:bstring,sha1:bstring,sha256:bstring,extracted:bstring,extracted_cutoff:bool,extracted_size:uint64,_write_ts:time};
type ftp={_path:string,ts:time,uid:bstring,id:conn_id,user:bstring,password:bstring,command:bstring,arg:bstring,mime_type:bstring,file_size:uint64,reply_code:uint64,reply_msg:bstring,data_channel:{passive:bool,orig_h:ip,resp_h:ip,resp_p:port},fuid:bstring,_write_ts:time};
type http={_path:string,ts:time,uid:bstring,id:conn_id,trans_depth:uint64,method:bstring,host:bstring,uri:bstring,referrer:bstring,version:bstring,user_agent:bstring,origin:bstring,request_body_len:uint64,response_body_len:uint64,status_code:uint64,status_msg:bstring,info_code:uint64,info_msg:bstring,tags:|[zenum]|,username:bstring,password:bstring,proxied:|[bstring]|,orig_fuids:[bstring],orig_filenames:[bstring],orig_mime_types:[bstring],resp_fuids:[bstring],resp_filenames:[bstring],resp_mime_types:[bstring],_write_ts:time};
type intel={_path:string,ts:time,uid:bstring,id:conn_id,seen:{indicator:bstring,indicator_type:zenum,where:zenum,node:bstring},matched:|[zenum]|,sources:|[bstring]|,fuid:bstring,file_mime_type:bstring,file_desc:bstring,_write_ts:time};
type irc={_path:string,ts:time,uid:bstring,id:conn_id,nick:bstring,user:bstring,command:bstring,value:bstring,addl:bstring,dcc_file_name:bstring,dcc_file_size:uint64,dcc_mime_type:bstring,fuid:bstring,_write_ts:time};
type kerberos={_path:string,ts:time,uid:bstring,id:conn_id,request_type:bstring,client:bstring,service:bstring,success:bool,error_msg:bstring,from:time,till:time,cipher:bstring,forwardable:bool,renewable:bool,client_cert_subject:bstring,client_cert_fuid:bstring,server_cert_subject:bstring,server_cert_fuid:bstring,_write_ts:time};
type known_certs={_path:string,ts:time,host:ip,port_num:port,subject:bstring,issuer_subject:bstring,serial:bstring,_write_ts:time};
type known_hosts={_path:string,ts:time,host:ip,_write_ts:time};
type known_services={_path:string,ts:time,host:ip,port_num:port,port_proto:zenum,service:|[bstring]|,_write_ts:time};
type loaded_scripts={_path:string,name:bstring,_write_ts:time};
type modbus={_path:string,ts:time,uid:bstring,id:conn_id,func:bstring,exception:bstring,_write_ts:time};
type mysql={_path:string,ts:time,uid:bstring,id:conn_id,cmd:bstring,arg:bstring,success:bool,rows:uint64,response:bstring,_write_ts:time};
type netcontrol={_path:string,ts:time,rule_id:bstring,category:zenum,cmd:bstring,state:zenum,action:bstring,target:zenum,entity_type:bstring,entity:bstring,mod:bstring,msg:bstring,priority:int64,expire:duration,location:bstring,plugin:bstring,_write_ts:time};
type netcontrol_drop={_path:string,ts:time,rule_id:bstring,orig_h:ip,orig_p:port,resp_h:ip,resp_p:port,expire:duration,location:bstring,_write_ts:time};
type netcontrol_shunt={_path:string,ts:time,rule_id:bstring,f:{src_h:ip,src_p:port,dst_h:ip,dst_p:port},expire:duration,location:bstring,_write_ts:time};
type notice={_path:string,ts:time,uid:bstring,id:conn_id,fuid:bstring,file_mime_type:bstring,file_desc:bstring,proto:zenum,note:zenum,msg:bstring,sub:bstring,src:ip,dst:ip,p:port,n:uint64,peer_descr:bstring,actions:|[zenum]|,email_dest:|[bstring]|,suppress_for:duration,remote_location:{country_code:bstring,region:bstring,city:bstring,latitude:float64,longitude:float64},_write_ts:time};
type notice_alarm={_path:string,ts:time,uid:bstring,id:conn_id,fuid:bstring,file_mime_type:bstring,file_desc:bstring,proto:zenum,note:zenum,msg:bstring,sub:bstring,src:ip,dst:ip,p:port,n:uint64,peer_descr:bstring,actions:|[zenum]|,email_dest:|[bstring]|,suppress_for:duration,remote_location:{country_code:bstring,region:bstring,city:bstring,latitude:float64,longitude:float64},_write_ts:time};
type ntlm={_path:string,ts:time,uid:bstring,id:conn_id,username:bstring,hostname:bstring,domainname:bstring,server_nb_computer_name:bstring,server_dns_computer_name:bstring,server_tree_name:bstring,success:bool,_write_ts:time};
type ntp={_path:string,ts:time,uid:bstring,id:conn_id,version:uint64,mode:uint64,stratum:uint64,poll:duration,precision:duration,root_delay:duration,root_disp:duration,ref_id:bstring,ref_time:time,org_time:time,rec_time:time,xmt_time:time,num_exts:uint64,_write_ts:time};
type ocsp={_path:string,ts:time,id:bstring,hashAlgorithm:bstring,issuerNameHash:bstring,issuerKeyHash:bstring,serialNumber:bstring,certStatus:bstring,revoketime:time,revokereason:bstring,thisUpdate:time,nextUpdate:time,_write_ts:time}
type openflow={_path:string,ts:time,dpid:uint64,match:{in_port:uint64,dl_src:bstring,dl_dst:bstring,dl_vlan:uint64,dl_vlan_pcp:uint64,dl_type:uint64,nw_tos:uint64,nw_proto:uint64,nw_src:net,nw_dst:net,tp_src:uint64,tp_dst:uint64},flow_mod:{cookie:uint64,table_id:uint64,command:zenum=(string),idle_timeout:uint64,hard_timeout:uint64,priority:uint64,out_port:uint64,out_group:uint64,flags:uint64,actions:{out_ports:[uint64],vlan_vid:uint64,vlan_pcp:uint64,vlan_strip:bool,dl_src:bstring,dl_dst:bstring,nw_tos:uint64,nw_src:ip,nw_dst:ip,tp_src:uint64,tp_dst:uint64}},_write_ts:time};
type packet_filter={_path:string,ts:time,node:bstring,filter:bstring,init:bool,success:bool,_write_ts:time};
type pe={_path:string,ts:time,id:bstring,machine:bstring,compile_ts:time,os:bstring,subsystem:bstring,is_exe:bool,is_64bit:bool,uses_aslr:bool,uses_dep:bool,uses_code_integrity:bool,uses_seh:bool,has_import_table:bool,has_export_table:bool,has_cert_table:bool,has_debug_data:bool,section_names:[bstring],_write_ts:time};
type radius={_path:string,ts:time,uid:bstring,id:conn_id,username:bstring,mac:bstring,framed_addr:ip,tunnel_client:bstring,connect_info:bstring,reply_msg:bstring,result:bstring,ttl:duration,_write_ts:time};
type rdp={_path:string,ts:time,uid:bstring,id:conn_id,cookie:bstring,result:bstring,security_protocol:bstring,client_channels:[bstring],keyboard_layout:bstring,client_build:bstring,client_name:bstring,client_dig_product_id:bstring,desktop_width:uint64,desktop_height:uint64,requested_color_depth:bstring,cert_type:bstring,cert_count:uint64,cert_permanent:bool,encryption_level:bstring,encryption_method:bstring,_write_ts:time};
type reporter={_path:string,ts:time,level:zenum,message:bstring,location:bstring,_write_ts:time};
type rfb={_path:string,ts:time,uid:bstring,id:conn_id,client_major_version:bstring,client_minor_version:bstring,server_major_version:bstring,server_minor_version:bstring,authentication_method:bstring,auth:bool,share_flag:bool,desktop_name:bstring,width:uint64,height:uint64,_write_ts:time};
type signatures={_path:string,ts:time,uid:bstring,src_addr:ip,src_port:port,dst_addr:ip,dst_port:port,note:zenum,sig_id:bstring,event_msg:bstring,sub_msg:bstring,sig_count:uint64,host_count:uint64,_write_ts:time};
type sip={_path:string,ts:time,uid:bstring,id:conn_id,trans_depth:uint64,method:bstring,uri:bstring,date:bstring,request_from:bstring,request_to:bstring,response_from:bstring,response_to:bstring,reply_to:bstring,call_id:bstring,seq:bstring,subject:bstring,request_path:[bstring],response_path:[bstring],user_agent:bstring,status_code:uint64,status_msg:bstring,warning:bstring,request_body_len:uint64,response_body_len:uint64,content_type:bstring,_write_ts:time};
type smb_files={_path:string,ts:time,uid:bstring,id:conn_id,fuid:bstring,action:zenum,path:bstring,name:bstring,size:uint64,prev_name:bstring,times:{modified:time,accessed:time,created:time,changed:time},_write_ts:time};
type smb_mapping={_path:string,ts:time,uid:bstring,id:conn_id,path:bstring,service:bstring,native_file_system:bstring,share_type:bstring,_write_ts:time};
type smtp={_path:string,ts:time,uid:bstring,id:conn_id,trans_depth:uint64,helo:bstring,mailfrom:bstring,rcptto:|[bstring]|,date:bstring,from:bstring,to:|[bstring]|,cc:|[bstring]|,reply_to:bstring,msg_id:bstring,in_reply_to:bstring,subject:bstring,x_originating_ip:ip,first_received:bstring,second_received:bstring,last_reply:bstring,path:[ip],user_agent:bstring,tls:bool,fuids:[bstring],is_webmail:bool,_write_ts:time};
type snmp={_path:string,ts:time,uid:bstring,id:conn_id,duration:duration,version:bstring,community:bstring,get_requests:uint64,get_bulk_requests:uint64,get_responses:uint64,set_requests:uint64,display_string:bstring,up_since:time,_write_ts:time};
type socks={_path:string,ts:time,uid:bstring,id:conn_id,version:uint64,user:bstring,password:bstring,status:bstring,request:{host:ip,name:bstring},request_p:port,bound:{host:ip,name:bstring},bound_p:port,_write_ts:time};
type software={_path:string,ts:time,host:ip,host_p:port,software_type:zenum,name:bstring,version:{major:uint64,minor:uint64,minor2:uint64,minor3:uint64,addl:bstring},unparsed_version:bstring,_write_ts:time};
type ssh={_path:string,ts:time,uid:bstring,id:conn_id,version:uint64,auth_success:bool,auth_attempts:uint64,direction:zenum,client:bstring,server:bstring,cipher_alg:bstring,mac_alg:bstring,compression_alg:bstring,kex_alg:bstring,host_key_alg:bstring,host_key:bstring,remote_location:{country_code:bstring,region:bstring,city:bstring,latitude:float64,longitude:float64},_write_ts:time};
type ssl={_path:string,ts:time,uid:bstring,id:conn_id,version:bstring,cipher:bstring,curve:bstring,server_name:bstring,resumed:bool,last_alert:bstring,next_protocol:bstring,established:bool,cert_chain_fuids:[bstring],client_cert_chain_fuids:[bstring],subject:bstring,issuer:bstring,client_subject:bstring,client_issuer:bstring,validation_status:bstring,_write_ts:time};
type stats={_path:string,ts:time,peer:bstring,mem:uint64,pkts_proc:uint64,bytes_recv:uint64,pkts_dropped:uint64,pkts_link:uint64,pkt_lag:duration,events_proc:uint64,events_queued:uint64,active_tcp_conns:uint64,active_udp_conns:uint64,active_icmp_conns:uint64,tcp_conns:uint64,udp_conns:uint64,icmp_conns:uint64,timers:uint64,active_timers:uint64,files:uint64,active_files:uint64,dns_requests:uint64,active_dns_requests:uint64,reassem_tcp_size:uint64,reassem_file_size:uint64,reassem_frag_size:uint64,reassem_unknown_size:uint64,_write_ts:time};
type syslog={_path:string,ts:time,uid:bstring,id:conn_id,proto:zenum,facility:bstring,severity:bstring,message:bstring,_write_ts:time};
type tunnel={_path:string,ts:time,uid:bstring,id:conn_id,tunnel_type:zenum,action:zenum,_write_ts:time};
type weird={_path:string,ts:time,uid:bstring,id:conn_id,name:bstring,addl:bstring,notice:bool,peer:bstring,source:bstring,_write_ts:time};
type x509={_path:string,ts:time,id:bstring,certificate:{version:uint64,serial:bstring,subject:bstring,issuer:bstring,not_valid_before:time,not_valid_after:time,key_alg:bstring,sig_alg:bstring,key_type:bstring,key_length:uint64,exponent:bstring,curve:bstring},san:{dns:[bstring],uri:[bstring],email:[bstring],ip:[ip]},basic_constraints:{ca:bool,path_len:uint64},_write_ts:time};

// This second block of type definitions represent changes needed to cover
// an out-of-the-box Zeek v4.1.0. In other Zeek revisions, we were accustomed
// to only seeing new fields added, but this represented the first time fields
// have changed, e.g. in SSL logs, "cert_chain_fuids" became "cert_chain_fps".
// Therefore we have wholly separate type definitions for this revision so we
// can cover 100% of the expected fields.

type ssl_4_1_0={_path:string,ts:time,uid:bstring,id:conn_id,version:bstring,cipher:bstring,curve:bstring,server_name:bstring,resumed:bool,last_alert:bstring,next_protocol:bstring,established:bool,ssl_history:bstring,cert_chain_fps:[bstring],client_cert_chain_fps:[bstring],subject:bstring,issuer:bstring,client_subject:bstring,client_issuer:bstring,sni_matches_cert:bool,validation_status:bstring,_write_ts:time};
type x509_4_1_0={_path:string,ts:time,fingerprint:bstring,certificate:{version:uint64,serial:bstring,subject:bstring,issuer:bstring,not_valid_before:time,not_valid_after:time,key_alg:bstring,sig_alg:bstring,key_type:bstring,key_length:uint64,exponent:bstring,curve:bstring},san:{dns:[bstring],uri:[bstring],email:[bstring],ip:[ip]},basic_constraints:{ca:bool,path_len:uint64},host_cert:bool,client_cert:bool,_write_ts:time};

const schemas = |{
  "broker": broker,
  "capture_loss": capture_loss,
  "cluster": cluster,
  "config": config,
  "conn": conn,
  "dce_rpc": dce_rpc,
  "dhcp": dhcp,
  "dnp3": dnp3,
  "dns": dns,
  "dpd": dpd,
  "files": files,
  "ftp": ftp,
  "http": http,
  "intel": intel,
  "irc": irc,
  "kerberos": kerberos,
  "known_certs": known_certs,
  "known_hosts": known_hosts,
  "known_services": known_services,
  "loaded_scripts": loaded_scripts,
  "modbus": modbus,
  "mysql": mysql,
  "netcontrol": netcontrol,
  "netcontrol_drop": netcontrol_drop,
  "netcontrol_shunt": netcontrol_shunt,
  "notice": notice,
  "notice_alarm": notice_alarm,
  "ntlm": ntlm,
  "ntp": ntp,
  "openflow": openflow,
  "packet_filter": packet_filter,
  "pe": pe,
  "radius": radius,
  "rdp": rdp,
  "reporter": reporter,
  "rfb": rfb,
  "signatures": signatures,
  "sip": sip,
  "smb_files": smb_files,
  "smb_mapping": smb_mapping,
  "smtp": smtp,
  "snmp": snmp,
  "socks": socks,
  "software": software,
  "ssh": ssh,
  "ssl": ssl,
  "stats": stats,
  "syslog": syslog,
  "tunnel": tunnel,
  "weird": weird,
  "x509": x509
}|

// We'll check for the presence of fields we know are unique to records that
// changed in Zeek v4.1.0 and shape those with special v4.1.0-specific config.
// For everything else we'll apply the type definitions that were known to
// cover Zeek v4.0.3 and earlier.

put this := unflatten(this) | switch (
  _path=="ssl" has(ssl_history) => put this := shape(ssl_4_1_0);
  _path=="x509" has(fingerprint) => put this := shape(x509_4_1_0);
  default => put this := shape(schemas[_path]);
)
