#define DQUOTE \x22
#define SQUOTE \x27

#ifdef GO
{
    package zql
}
#define RETURN(x) return x, nil
#define VAR(x) var x
#define NULL nil
#define ARRAY(...) []interface{}{__VA_ARGS__}
#define ARRAY_LEN(a) len(a.([]interface{}))
#define FOREACH(arr, var) for _, var := range arr
#define APPEND(arr, value) arr = append(arr, value)
#define PREPEND(value, arr) append([]interface{}{value}, (arr.([]interface{}))...)
#define TEXT string(c.text)
#define TOSTRING(s) fmt.Sprintf("%v", s)
#define ISNOTNULL(n) n != nil
#define ASSERT_ARRAY(a) a.([]interface{})
#define ASSERT_STRING(s) s.(string)
#define ASSERT_INT(i) i.(int)
#define ASSERT_MAP(m) m.(map[string]interface{})
#define HAS(m, k) _, ok := m[k]; ok
#define MAP(...) map[string]interface{}{__VA_ARGS__}
#define PRINT(...) fmt.Println(__VA_ARGS__)
#else
{
#include "parser-support.js"
}
#define RETURN(x) return x
#define VAR(x) let x
#define NULL null
#define ARRAY(...) [__VA_ARGS__]
#define ARRAY_LEN(a) a.length
#define FOREACH(arr, var) for(let var of arr)
#define APPEND(arr, value) arr.push(value)
#define PREPEND(value, arr) [value, ...arr]
#define TEXT text()
#define ASSERT_INT(i) i
#define TOSTRING(s) s.toString()
#define ISNOTNULL(n) (n)
#define ASSERT_ARRAY(a) a
#define ASSERT_STRING(s) s
#define ASSERT_MAP(m) m
#define HAS(m, k) (k in m)
#define MAP(...) {__VA_ARGS__}
#define PRINT(...) console.log(__VAR_ARGS__)
#endif

start = __ ast:Z __ EOF { RETURN(ast) }

// A Z script can be either a single operation or a chain of operations.
// For a single operation, we do not need to wrap operator in a SequentialProc,
// but we do for everything but FilterProc because the zq runtime is fragile
// to this difference right now.  See issue #1809.
Z // = !(Operator / Aggregation / "(") op:Operation &EOF { RETURN(op) }
  = Sequential

Sequential
  = first:Operation rest:SequentialTail+ {
      RETURN(MAP("op": "SequentialProc", "procs": PREPEND(first, rest)))
    }
  / op:Operation {
      RETURN(MAP("op": "SequentialProc", "procs": ARRAY(op)))
    }

SequentialTail = __ "|" __ p:Operation { RETURN(p) }

Parallel
  = first:Sequential rest:ParallelTail+ {
      RETURN(PREPEND(first, rest))
    }
  / first:Sequential {
      RETURN(ARRAY(first))
    }

ParallelTail
  = __ "=>" __ ch:Sequential { RETURN(ch) }

Operation
  = "split" __ "(" __ "=>" __ procArray:Parallel __ ")" {
      RETURN(MAP("op": "ParallelProc", "procs": procArray))
    }
  / Operator
  / a:Aggregation !ExprGuard  { RETURN(a) }
  / expr:SearchBoolean !AggGuard {
      RETURN(MAP("op": "FilterProc", "filter": expr))
    }

ExprGuard = __ ((!"=>" Comparator) / AdditiveOperator / MultiplicativeOperator / ":" / "(")
AggGuard = (_ (ByToken / "-with") EOT) / (__ ",")

SearchBoolean
  = first:SearchAnd rest:SearchOrTerm* {
      RETURN(makeBinaryExprChain(first, rest))
    }

SearchOrTerm = _ OrToken _ t:SearchAnd { RETURN(ARRAY("or", t)) }

SearchAnd
  = first:SearchFactor
    rest:(__ (AndToken _)? __ expr:SearchFactor{ RETURN(ARRAY("and", expr)) })* {
      RETURN(makeBinaryExprChain(first,rest))
    }

SearchFactor
  = (NotToken _ / "!" __) e:SearchFactor {
      RETURN(MAP("op": "UnaryExpr", "operator": "!", "operand": e))
    }
  / ShortCut
  / SearchExpr
  / "(" __ expr:SearchBoolean __ ")" { RETURN(expr) }

ShortCut
  = "*" __ compareOp:EqualityToken __ v:SearchValue {
      RETURN(MAP("op": "FunctionCall", "function": "or",
        "args": ARRAY(MAP("op": "BinaryExpr", "operator":"@",
                 "lhs":MAP("op": "SelectExpr", "selectors": ARRAY(MAP("op": "RootRecord"))),
                 "rhs":MAP("op": "FunctionCall", "function": "map",
                   "args": ARRAY(MAP("op": "BinaryExpr", "operator": "=",
                     "lhs": MAP("op": "Identifier", "name": "$"),
                     "rhs": v)))))))
    }
  / f:Lval __ comp:EqualityToken __ v:GlobbySearchValue !ExprGuard {
      RETURN(MAP("op": "BinaryExpr", "operator":comp, "lhs":f, "rhs":v))
    }
  / v:SearchValue _ InToken _ "*" {
      RETURN(MAP("op": "FunctionCall", "function": "or",
        "args": ARRAY(MAP("op": "BinaryExpr", "operator":"@",
                 "lhs":MAP("op": "SelectExpr", "selectors": ARRAY(MAP("op": "RootRecord"))),
                 "rhs":MAP("op": "FunctionCall", "function": "map",
                   "args": ARRAY(MAP("op": "BinaryExpr", "operator": "in",
                     "lhs": v,
                     "rhs": MAP("op": "Identifier", "name": "$"))))))))
    }
  / !(SearchGuard EOT) v:GlobbySearchValue !ExprGuard {
      RETURN(MAP("op": "Search", "text": TEXT, "value": v))
    }
  / "*" !ExprGuard {
      RETURN(MAP("op": "Literal", "type": "bool", "value": "true"))
    }

SearchValue
  = Literal
  / v:KeyWord {
      RETURN(MAP("op": "Literal", "type": "string", "value": v))
    }

GlobbySearchValue
  = Literal
  / v:SearchGlob {
      VAR(str) = ASSERT_STRING(v)
      VAR(literal) = MAP("op": "Literal", "type": "string", "value": v)
      if (reglob.IsGlobby(str)) {
        literal["type"] = "regexp"
        literal["value"] = reglob.Reglob(str)
      }
      RETURN(literal)
    }

SearchGlob
  = head:GlobPart+ tail:("*")* {
      RETURN(joinChars(head) + joinChars(tail))
    }

GlobPart
  = s:Stars v:KeyWord { RETURN(ASSERT_STRING(s)+ASSERT_STRING(v)) }
  / KeyWord

Stars = "*"+ { RETURN(TEXT) }

SearchGuard
  = AndToken
  / OrToken
  / NotToken
  / InToken
  / ByToken

/// === Search-embedded Expression Context ===

SearchExpr = SearchExprRelative

Comparator = ("=" / "!=" / "in" / "<=" / "<" / ">=" / ">") { RETURN(TEXT) }

SearchExprRelative
  = first:SearchExprAdd
    rest:(__ op:Comparator __ expr:SearchExprAdd{ RETURN(ARRAY(op, expr)) })* {
        RETURN(makeBinaryExprChain(first, rest))
    }

SearchExprAdd
  = first:SearchExprMul
    rest:(__ op:AdditiveOperator __ expr:SearchExprMul{ RETURN(ARRAY(op, expr)) })* {
        RETURN(makeBinaryExprChain(first, rest))
    }

SearchExprMul
  = first:SearchExprCast
    rest:(__ op:MultiplicativeOperator __ expr:SearchExprCast{ RETURN(ARRAY(op, expr)) })* {
        RETURN(makeBinaryExprChain(first, rest))
    }

SearchExprCast
  = e:SearchExprFunc typ:( ":" typ:PrimitiveType { RETURN(typ) }) {
      RETURN(MAP("op": "CastExpr", "expr": e, "type": typ))
    }
  / SearchExprFunc

SearchExprFunc
  = MatchExpr
  / first:Function rest:(Deref)*  {
      RETURN(makeBinaryExprChain(first, rest))
    }
  / DerefExpr
  / Literal

/// === Aggregations ===

Aggregation
  = every:EveryDur? keys:GroupByKeys limit:LimitArg {
      RETURN(MAP("op": "GroupByProc", "keys": keys, "reducers": NULL, "duration": every, "limit": limit))
    }
  / every:EveryDur? reducers:Reducers keys:(_ GroupByKeys)? limit:LimitArg? {
      VAR(p) = MAP("op": "GroupByProc", "keys": NULL, "reducers": reducers, "duration": every, "limit": limit)
      if ISNOTNULL(keys) {
        p["keys"] = ASSERT_ARRAY(keys)[1]
      }
      RETURN(p)
    }

EveryDur
  = "every"i _ dur:Duration _ { RETURN(dur) }

GroupByKeys
  = ByToken _ columns:FlexAssignments { RETURN(columns) }

LimitArg
  = _ "with" _ "-limit" _ limit:UInt { RETURN(limit) }
  / "" { RETURN(0) }

// A FlexAssignment is like an Assignment but it can optionally omit the lhs,
// in which case the semantic pass will infer a name from the rhs, e.g., for
// an expression like "count() by foo", the rhs is Field "foo" and the lhs is nil.
FlexAssignment
  = Assignment
  / expr:Expr { RETURN(MAP("op": "Assignment", "lhs": NULL, "rhs": expr)) }

FlexAssignments
  = first:FlexAssignment rest:(__ "," __ expr:FlexAssignment { RETURN(expr) })* {
      RETURN(PREPEND(first, rest))
    }

ReducerAssignment
  = lval:Lval __ "=" __ reducer:Reducer {
      RETURN(MAP("op": "Assignment", "lhs": lval, "rhs": reducer))
    }
  / reducer:Reducer {
      RETURN(MAP("op": "Assignment", "lhs": NULL, "rhs": reducer))
    }

Reducer
  = !(NotReducers __ "(") op:ReducerName __ "(" __ expr:Expr?  __ ")" !(__ ".") where:WhereClause? {
      VAR(r) = MAP("op": "Reducer", "operator": op, "expr": NULL, "where":where)
      if ISNOTNULL(expr) {
        r["expr"] = expr
      }
      RETURN(r)
    }

ReducerName
  = IdentifierName
  / AndToken
  / OrToken

NotReducers
  = "not"
  / "match"

WhereClause = _ "where" _ expr:SearchBoolean { RETURN(expr) }

Reducers
  = first:ReducerAssignment rest:(__ "," __ ReducerAssignment)* {
      VAR(result) = ARRAY(first)
      FOREACH(ASSERT_ARRAY(rest), r) {
        APPEND(result, ASSERT_ARRAY(r)[3])
      }
      RETURN(result)
    }

Operator
  = SortProc
  / TopProc
  / CutProc
  / PickProc
  / DropProc
  / HeadProc
  / TailProc
  / FilterProc
  / UniqProc
  / PutProc
  / RenameProc
  / FuseProc
  / JoinProc

SortProc
  = "sort"i args:SortArgs list:(_ l:Exprs { RETURN(l) })? {
      VAR(argm) = ASSERT_MAP(args)
      VAR(proc) = MAP("op": "SortProc", "fields": list, "sortdir": 1, "nullsfirst": false)
      if HAS(argm, "r") {
        proc["sortdir"] = -1
      }
      if HAS(argm, "nulls") {
        if (argm["nulls"] == "first") {
          proc["nullsfirst"] = true
        }
      }
      RETURN(proc)
    }

SortArgs = args:(_ a:SortArg{ RETURN(a) })* { return makeArgMap(args) }

SortArg
  = "-r" { RETURN(MAP("name": "r", "value": NULL)) }
  / "-nulls" _ where:(("first" / "last") { RETURN(TEXT) } ) { RETURN(MAP("name": "nulls", "value": where)) }

TopProc
  = "top"i limit:(_ n:UInt { RETURN(n)})? flush:(_ "-flush")? fields:(_ f:FieldExprs { RETURN(f) })? {
      VAR(proc) = MAP("op": "TopProc", "limit": 0, "fields": NULL, "flush": false)
      if ISNOTNULL(limit) {
        proc["limit"] = limit
      }
      if ISNOTNULL(fields) {
        proc["fields"] = fields
      }
      if ISNOTNULL(flush) {
        proc["flush"] = true
      }
      RETURN(proc)
    }

CutProc
  = "cut"i args:CutArgs _ columns:FlexAssignments {
      VAR(argm) = ASSERT_MAP(args)
      VAR(proc) = MAP("op": "CutProc", "fields": columns, "complement": false)
      if HAS(argm, "c") {
        proc["complement"] = true
      }
      RETURN(proc)
    }

CutArgs
  = args:(_ "-c" { RETURN(MAP("name": "c", "value": NULL)) })* {
      return makeArgMap(args)
    }

PickProc
  = "pick"i _ columns:FlexAssignments {
      RETURN(MAP("op": "PickProc", "fields": columns))
    }

DropProc
  = "drop"i _ columns:FieldExprs {
      RETURN(MAP("op": "DropProc", "fields": columns))
    }

HeadProc
  = "head"i _ count:UInt { RETURN(MAP("op": "HeadProc", "count": count)) }
  / "head"i { RETURN(MAP("op": "HeadProc", "count": 1)) }

TailProc
  = "tail"i _ count:UInt { RETURN(MAP("op": "TailProc", "count": count)) }
  / "tail"i { RETURN(MAP("op": "TailProc", "count": 1)) }

FilterProc
  = "filter"i _ op:Filter {
      RETURN(op)
    }

Filter
  = expr:SearchBoolean {
      RETURN(MAP("op": "FilterProc", "filter": expr))
    }

UniqProc
  = "uniq"i _ "-c" {
      RETURN(MAP("op": "UniqProc", "cflag": true))
    }
  / "uniq"i {
      RETURN(MAP("op": "UniqProc", "cflag": false))
    }

PutProc
  = "put"i _ columns:FlexAssignments {
      RETURN(MAP("op": "PutProc", "clauses": columns))
    }

RenameProc
  = "rename"i _ first:Assignment rest:(__ "," __ cl:Assignment { RETURN(cl) })* {
      RETURN(MAP("op": "RenameProc", "fields": PREPEND(first, rest)))
    }

FuseProc
  = "fuse"i {
      RETURN(MAP("op": "FuseProc"))
    }

JoinProc
  = "join"i _ leftKey:JoinKey __ "=" __ rightKey:JoinKey columns:(_ FlexAssignments)? {
      VAR(proc) = MAP("op": "JoinProc", "left_key": leftKey, "right_key": rightKey, "clauses": NULL)
      if ISNOTNULL(columns) {
        proc["clauses"] = ASSERT_ARRAY(columns)[1]
      }
      RETURN(proc)
    }
  / "join"i _ key:JoinKey columns:(_ FlexAssignments)? {
      VAR(proc) = MAP("op": "JoinProc", "left_key": key, "right_key": key, "clauses": NULL)
      if ISNOTNULL(columns) {
        proc["clauses"] = ASSERT_ARRAY(columns)[1]
      }
      RETURN(proc)
    }

JoinKey
  = Lval
  / "(" expr:Expr ")" { RETURN(expr) }

Lval = DerefExpr

FieldExpr = Lval

FieldExprs
  = first:FieldExpr rest:(__ "," __ FieldExpr)* {
      VAR(result) = ARRAY(first)

      FOREACH(ASSERT_ARRAY(rest), r) {
        APPEND(result, ASSERT_ARRAY(r)[3])
      }

      RETURN(result)
    }

Exprs
  = first:Expr rest:(__ "," __ Expr)* {
      VAR(result) = ARRAY(first)

      FOREACH(ASSERT_ARRAY(rest), r) {
        APPEND(result, ASSERT_ARRAY(r)[3])
      }

      RETURN(result)
    }

Assignment
  = lhs:Lval __ "=" __ rhs:Expr { RETURN(MAP("op": "Assignment", "lhs": lhs, "rhs": rhs)) }

Expr = ConditionalExpr

ConditionalExpr
  = condition:LogicalOrExpr __ "?" __ thenClause:Expr __ ":" __ elseClause:Expr {
      RETURN(MAP("op": "ConditionalExpr", "condition": condition, "then": thenClause, "else": elseClause))
    }
  / LogicalOrExpr

LogicalOrExpr
  = first:LogicalAndExpr
    rest:(__ op:OrToken __ expr:LogicalAndExpr{ RETURN(ARRAY(op, expr)) })* {
        RETURN(makeBinaryExprChain(first, rest))
    }

LogicalAndExpr
  = first:EqualityCompareExpr
    rest:(__ op:AndToken __ expr:EqualityCompareExpr{ RETURN(ARRAY(op, expr)) })* {
        RETURN(makeBinaryExprChain(first, rest))
    }

EqualityCompareExpr
  = first:RelativeExpr
    rest:(__ comp:EqualityComparator __ expr:RelativeExpr{ RETURN(ARRAY(comp, expr)) })* {
        RETURN(makeBinaryExprChain(first, rest))
    }

EqualityOperator
  = ("=" / "!=") { RETURN(TEXT) }

EqualityComparator
  = EqualityOperator
  / "in" { RETURN(TEXT) }

RelativeExpr
  = first:AdditiveExpr
    rest:(__ op:RelativeOperator __ expr:AdditiveExpr{ RETURN(ARRAY(op, expr)) })* {
        RETURN(makeBinaryExprChain(first, rest))
    }

RelativeOperator = ("<=" / "<" / ">=" / ">") { RETURN(TEXT) }

AdditiveExpr
  = first:MultiplicativeExpr
    rest:(__ op:AdditiveOperator __ expr:MultiplicativeExpr{ RETURN(ARRAY(op, expr)) })* {
        RETURN(makeBinaryExprChain(first, rest))
    }

AdditiveOperator = ("+" / "-") { RETURN(TEXT) }

MultiplicativeExpr
  = first:NotExpr
    rest:(__ op:MultiplicativeOperator __ expr:NotExpr{ RETURN(ARRAY(op, expr)) })* {
        RETURN(makeBinaryExprChain(first, rest))
    }

MultiplicativeOperator = ("*" / "/") { RETURN(TEXT) }

NotExpr
  = "!" __ e:NotExpr {
        RETURN(MAP("op": "UnaryExpr", "operator": "!", "operand": e))
    }
  / CastExpr

CastExpr
  = e:FuncExpr typ:( ":" typ:PrimitiveType { RETURN(typ) }) {
      RETURN(MAP("op": "CastExpr", "expr": e, "type": typ))
    }
  / FuncExpr


PrimitiveType
  = (   "bytes" / "uint8" / "uint16" / "uint32" / "uint64"
      / "int8" / "int16" / "int32" / "int64"
      / "duration" / "time"
      / "float64"
      / "bool" / "bytes" / "string" / "bstring"
      / "ip" / "net"
      / "type" / "error" / "null" ) { RETURN(TEXT) }

FuncExpr
  = SelectExpr
  / MatchExpr
  / first:Function rest:(Deref)* {
      RETURN(makeBinaryExprChain(first, rest))
    }
  / DerefExpr
  / Primary

MatchExpr
  = "match" __ "(" expr:SearchBoolean ")" { RETURN(expr) }

SelectExpr
  = "select" __ "(" __ args:ArgumentList __ ")" methods:(__ "." __ e:Function { RETURN(ARRAY("@", e)) })* {
      RETURN(makeBinaryExprChain(MAP("op":"SelectExpr", "selectors":args), methods))
    }

Function
  = fn:IdentifierName __ "(" __ args:ArgumentList __ ")" {
      RETURN(MAP("op": "FunctionCall", "function": fn, "args": args))
    }

ArgumentList
  = first:Expr rest:(__ "," __ e:Expr { RETURN(e) })* {
      RETURN(PREPEND(first, rest))
    }
  / __ { RETURN(ARRAY()) }

DerefExpr
  = first:DotId rest:(Deref)* {
      RETURN(makeBinaryExprChain(first, rest))
    }
  / first:Identifier rest:(Deref)* {
      RETURN(makeBinaryExprChain(first, rest))
    }
  / "." {
      RETURN(MAP("op":"RootRecord"))
    }

DotId
  = "." field:Identifier {
      RETURN(MAP("op": "BinaryExpr", "operator":".",
                     "lhs":MAP("op":"RootRecord"),
                     "rhs":field))
    }
  / "." "[" expr:Expr "]" {
      RETURN(MAP("op": "BinaryExpr", "operator":"[",
                     "lhs":MAP("op":"RootRecord"),
                     "rhs":expr))
    }

Deref
  = "[" expr:Expr "]" { RETURN(ARRAY("[", expr)) }
  / "." !(".") id:Identifier { RETURN(ARRAY(".", id)) }

Primary
  = Literal
  / "(" __ expr:Expr __ ")" { RETURN(expr) }

Literal
  = StringLiteral
  / RegexpLiteral
  / SubnetLiteral
  / AddressLiteral
  / FloatLiteral
  / IntegerLiteral
  / BooleanLiteral
  / NullLiteral

StringLiteral
  = v:QuotedString {
      RETURN(MAP("op": "Literal", "type": "string", "value": v))
    }

RegexpLiteral
  = v:Regexp !KeyWordStart {
      RETURN(MAP("op": "Literal", "type": "regexp", "value": v))
    }

SubnetLiteral
  = v:IP6Net !IdentifierRest {
      RETURN(MAP("op": "Literal", "type": "net", "value": v))
    }
  / v:IP4Net {
      RETURN(MAP("op": "Literal", "type": "net", "value": v))
    }

AddressLiteral
  = v:IP6 !IdentifierRest {
      RETURN(MAP("op": "Literal", "type": "ip", "value": v))
    }
  / v:IP {
      RETURN(MAP("op": "Literal", "type": "ip", "value": v))
    }

FloatLiteral
  = v:FloatString {
      RETURN(MAP("op": "Literal", "type": "float64", "value": v))
    }

IntegerLiteral
  = v:IntString {
      RETURN(MAP("op": "Literal", "type": "int64", "value": v))
    }

BooleanLiteral
  = "true"           { RETURN(MAP("op": "Literal", "type": "bool", "value": "true")) }
  / "false"          { RETURN(MAP("op": "Literal", "type": "bool", "value": "false")) }

NullLiteral
  = "null"           { RETURN(MAP("op": "Literal", "type": "null", "value": "")) }

EqualityToken
  = EqualityOperator / RelativeOperator

AndToken = "and"i { RETURN("and") }
OrToken = "or"i { RETURN("or") }
InToken = "in"i { RETURN("in") }
NotToken = "not"i { RETURN("not") }
ByToken = "by"i { RETURN("by") }

IdentifierStart = [A-Za-z_$]
IdentifierRest = IdentifierStart / [0-9]

Identifier
  = id:IdentifierName { RETURN(MAP("op": "Identifier", "name": id)) }

IdentifierName
  = !(IdGuard !IdentifierRest) IdentifierStart IdentifierRest* {  RETURN(TEXT) }
  / "$" { RETURN(TEXT) }
  / "\\" id:IdGuard { RETURN(id) }

IdGuard
  = BooleanLiteral
  / NullLiteral
  / SearchGuard

Duration
  = Seconds
  / Minutes
  / Hours
  / Hours _ "and" _ Minutes
  / Days
  / Weeks

SecondsToken
  = "seconds"
  / "second"
  / "secs"
  / "sec"
  / "s"

MinutesToken
  = "minutes"
  / "minute"
  / "mins"
  / "min"
  / "m"

HoursToken
  = "hours"
  / "hrs"
  / "hr"
  / "h"
  / "hour"

DaysToken = "days"/"day"/"d"
WeeksToken = "weeks"/"week"/"wks"/"wk"/"w"

Seconds
  = "second" { RETURN(MAP("type": "Duration", "seconds": 1)) }
  / num:UInt __ SecondsToken { RETURN(MAP("type": "Duration", "seconds": num)) }

Minutes
  = "minute" { RETURN(MAP("type": "Duration", "seconds": 60)) }
  / num:UInt __ MinutesToken { RETURN(MAP("type": "Duration", "seconds": ASSERT_INT(num)*60)) }

Hours
  = "hour" { RETURN(MAP("type": "Duration", "seconds": 3600)) }
  / num:UInt __ HoursToken { RETURN(MAP("type": "Duration", "seconds": ASSERT_INT(num)*3600)) }

Days
  = "day" { RETURN(MAP("type": "Duration", "seconds": 3600*24)) }
  / num:UInt __ DaysToken { RETURN(MAP("type": "Duration", "seconds": (ASSERT_INT(num)*3600*24))) }

Weeks
  = "week" { RETURN(MAP("type": "Duration", "seconds": 3600*24*7)) }
  / num:UInt __ WeeksToken { RETURN(MAP("type": "Duration", "seconds": ASSERT_INT(num)*3600*24*7)) }

//XXX what about mac addrs?
IP
  = UInt "." UInt "." UInt "." UInt { RETURN(TEXT) }

// this matches a superset of legal syntax for ip6 addresses but the compiler
// will catch any errors when translating the filter
IP6
  = a:HexColon+ b:IP6Tail {
      RETURN(joinChars(a) + ASSERT_STRING(b))
    }
  / a:Hex b:ColonHex* "::" d:HexColon* e:IP6Tail {
      RETURN(ASSERT_STRING(a) + joinChars(b) + "::" + joinChars(d) + ASSERT_STRING(e))
    }
  / "::" a:HexColon* b:IP6Tail {
      RETURN("::" + joinChars(a) + ASSERT_STRING(b))
    }
  / a:Hex b:ColonHex* "::" {
      RETURN(ASSERT_STRING(a) + joinChars(b) + "::")
    }
  / "::" {
      RETURN("::")
    }

IP6Tail
  = IP
  / Hex

ColonHex = ":" v:Hex { RETURN(":" + ASSERT_STRING(v)) }
HexColon = v:Hex ":" { RETURN(ASSERT_STRING(v) + ":") }

IP4Net
  = a:IP '/' m:UInt {
      RETURN(ASSERT_STRING(a) + "/" + TOSTRING(m));
    }

IP6Net
  = a:IP6 '/' m:UInt {
      RETURN(ASSERT_STRING(a) + "/" + ASSERT_STRING(m));
    }

UInt
 = s:UIntString { RETURN(parseInt(s)) }

IntString
  = UIntString
  / MinusIntString

UIntString = [0-9]+ { RETURN(TEXT) }

MinusIntString
  = "-" UIntString { RETURN(TEXT) }

FloatString
  = "-"? [0-9]+ "." [0-9]+ ExponentPart? {
      RETURN(TEXT)
    }
  / "-"? "." [0-9]+ ExponentPart? {
      RETURN(TEXT)
    }

ExponentPart = "e"i [+-]? UIntString

Hex = HexDigit+ { RETURN(TEXT) }

HexDigit = [0-9a-fA-F]


QuotedString
  = '"' v:DoubleQuotedChar* '"' { RETURN(joinChars(v)) }
  / "'" v:SingleQuotedChar* "'" { RETURN(joinChars(v)) }

DoubleQuotedChar
  = !('"' / EscapedChar) . { RETURN(TEXT) }
  / "\\" s:EscapeSequence { RETURN(s) }

KeyWord
  = head:KeyWordStart tail:KeyWordRest* { RETURN(ASSERT_STRING(head) + joinChars(tail)) }

KeyWordStart
  = [a-zA-Z_.:/%#@~] { RETURN(TEXT) }
  / KeyWordEsc

KeyWordRest
  = KeyWordStart
  / [0-9]

KeyWordEsc = "\\" s:(KeywordEscape / EscapeSequence)  { RETURN(s) }

SingleQuotedChar
  = !("'" / EscapedChar) . { RETURN(TEXT) }
  / "\\" s:EscapeSequence { RETURN(s) }

EscapeSequence
  = "x" HexDigit HexDigit { RETURN("\\" + TEXT) }
  / SingleCharEscape
  / UnicodeEscape

SingleCharEscape
  = "'" { RETURN("'")}
  / '"' { RETURN('"')}
  / "\\" { RETURN("\\")}
  / "b" { RETURN("\b") }
  / "f" { RETURN("\f") }
  / "n" { RETURN("\n") }
  / "r" { RETURN("\r") }
  / "t" { RETURN("\t") }
  / "v" { RETURN("\v") }

KeywordEscape
  = "=" { RETURN("=") }
  / "*" { RETURN("\\*") }
  / [+-]

UnicodeEscape
  = "u" chars:(HexDigit HexDigit HexDigit HexDigit) {
      RETURN(makeUnicodeChar(chars))
    }
  / "u" "{" chars:(HexDigit HexDigit? HexDigit? HexDigit? HexDigit? HexDigit?) "}" {
      RETURN(makeUnicodeChar(chars))
    }

Regexp
  = "/" body:RegexpBody "/" { RETURN(body) }

RegexpBody
  = ([^/\\]/"\\/")+ { RETURN(TEXT) }

EscapedChar
  = [\x00-\x1f\\]

_  = AnySpace+
__ = AnySpace*

AnySpace
  = WhiteSpace
  / LineTerminator
  / Comment

SourceCharacter
  = .

WhiteSpace "whitespace"
  = "\t"
  / "\v"
  / "\f"
  / " "
  / "\u00A0"
  / "\uFEFF"

LineTerminator
  = [\n\r\u2028\u2029]

// XXX We will leave multi-line comments out for now since there is some work
// that needs to be done disambiguating among KeyWord, Regexp, and "/*".
// We will tackle this in the search-expr branch that is changing the grammar
// to better mix together ad hoc keyword search with formal boolean expressions.
Comment "comment"
  // = MultiLineComment
  // / SingleLineComment
  = SingleLineComment

MultiLineComment
  = "/*" (!"*/" SourceCharacter)* "*/"

SingleLineComment
  = "//" (!LineTerminator SourceCharacter)*

EOT = _ / EOF
EOF = !.
