#define DQUOTE \x22
#define SQUOTE \x27

#ifdef GO
{
    package zql
}
#define RETURN(x) return x, nil
#define VAR(x) var x
#define NULL nil
#define ARRAY(...) []interface{}{__VA_ARGS__}
#define ARRAY_LEN(a) len(a.([]interface{}))
#define FOREACH(arr, var) for _, var := range arr
#define APPEND(arr, value) arr = append(arr, value)
#define PREPEND(value, arr) append([]interface{}{value}, (arr.([]interface{}))...)
#define TEXT string(c.text)
#define TOSTRING(s) fmt.Sprintf("%v", s)
#define ISNOTNULL(n) n != nil
#define ASSERT_ARRAY(a) a.([]interface{})
#define ASSERT_STRING(s) s.(string)
#define ASSERT_INT(i) i.(int)
#define ASSERT_MAP(m) m.(map[string]interface{})
#define HAS(m, k) _, ok := m[k]; ok
#define MAP(...) map[string]interface{}{__VA_ARGS__}
#define PRINT(...) fmt.Println(__VA_ARGS__)
#else
{
#include "parser-support.js"
}
#define RETURN(x) return x
#define VAR(x) let x
#define NULL null
#define ARRAY(...) [__VA_ARGS__]
#define ARRAY_LEN(a) a.length
#define FOREACH(arr, var) for(let var of arr)
#define APPEND(arr, value) arr.push(value)
#define PREPEND(value, arr) [value, ...arr]
#define TEXT text()
#define ASSERT_INT(i) i
#define TOSTRING(s) s.toString()
#define ISNOTNULL(n) (n)
#define ASSERT_ARRAY(a) a
#define ASSERT_STRING(s) s
#define ASSERT_MAP(m) m
#define HAS(m, k) (k in m)
#define MAP(...) {__VA_ARGS__}
#define PRINT(...) console.log(__VAR_ARGS__)
#endif

start = __ ast:query __ EOF { RETURN(ast) }

query
  = procs:procChain {
      VAR(filt) = MAP("op": "FilterProc", "filter": MAP("op": "MatchAll"))
      RETURN(MAP("op": "SequentialProc", "procs": PREPEND(filt, procs)))
    }
  / s:search __ rest:chainedProc* {
      if (ARRAY_LEN(rest) == 0) {
          RETURN(s)
      } else {
          RETURN(MAP("op": "SequentialProc", "procs": PREPEND(s, rest)))
      }
    }
  / s:search {
      RETURN(MAP("op": "SequentialProc", "procs": ARRAY(s)))
    }

procChain
  = first:proc rest:chainedProc* {
      if ISNOTNULL(rest) {
        RETURN(PREPEND(first, rest))
      } else {
        RETURN(ARRAY(first))
      }
    }

chainedProc = __ "|" __ p:proc { RETURN(p) }

search
  = expr:searchExpr {
      RETURN(MAP("op": "FilterProc", "filter": expr))
    }

searchExpr
  = first:searchTerm rest:oredSearchTerm* {
      RETURN(makeChain(first, rest, "LogicalOr"))
    }

oredSearchTerm = _ orToken _ t:searchTerm { RETURN(t) }

searchTerm
  = first:searchFactor rest:andedSearchTerm* {
      RETURN(makeChain(first, rest, "LogicalAnd"))
    }

andedSearchTerm = _ (andToken _)? f:searchFactor { RETURN(f) }

searchFactor
  = (notToken _ / "!" __) e:searchExpr {
      RETURN(MAP("op": "LogicalNot", "expr": e))
    }
  / !("-") s:searchPred { RETURN(s) }
  / "(" __ expr:searchExpr __ ")" { RETURN(expr) }

searchPred
  = "*" __ comp:equalityToken __ v:searchValue {
      RETURN(MAP("op": "CompareAny", "comparator": comp, "recursive": false, "value": v))
    }
  / "**" __ comp:equalityToken __ v:searchValue {
      RETURN(MAP("op": "CompareAny", "comparator": comp, "recursive": true, "value": v))
    }
  / f:Lval __ comp:equalityToken __ v:searchValue {
      RETURN(MAP("op": "CompareField", "comparator": comp, "field": f, "value": v))
    }
  / expr:FunctionExpr __ comp:equalityToken __ v:searchValue {
      RETURN(MAP("op": "BinaryExpression", "operator": comp, "lhs": expr, "rhs": v))
    }
  / v:searchValue __ inToken __ "*" {
      RETURN(MAP("op": "CompareAny", "comparator": "in", "recursive": false, "value": v))
    }
  / v:searchValue __ inToken __ f:fieldExpr {
      RETURN(MAP("op": "CompareField", "comparator": "in", "field": f, "value": v))
    }
  / v:searchLiteral {
      RETURN(MAP("op": "Search", "text": TEXT, "value": v))
    }
  / !((searchKeywords) _ ) v:searchWord {
      VAR(str) = ASSERT_STRING(v)
      if (str == "*") {
        RETURN(MAP("op": "MatchAll"))
      }
      VAR(literal) = MAP("op": "Literal", "type": "string", "value": v)
      if (reglob.IsGlobby(str)) {
        literal["type"] = "regexp"
        literal["value"] = reglob.Reglob(str)
      }
      RETURN(MAP("op": "Search", "text": TEXT, "value": literal))
    }

searchLiteral
  = StringLiteral
  / RegexpLiteral
  / SubnetLiteral
  / AddressLiteral
  / FloatLiteral

  // Careful not to use IntegerLiteral unconditionally or it will consume
  // the beginning of something like 1234abcd which is a valid search word
  / i:IntegerLiteral !searchWord { RETURN(i) }

  / !(searchKeywords _) v:BooleanLiteral { RETURN(v) }
  / !(searchKeywords _) v:NullLiteral { RETURN(v) }


searchValue
  = searchLiteral
  / !((searchKeywords) _) v:searchWord {
      RETURN(MAP("op": "Literal", "type": "string", "value": v))
    }

StringLiteral
  = v:quotedString {
      RETURN(MAP("op": "Literal", "type": "string", "value": v))
    }

RegexpLiteral
  = v:reString {
      RETURN(MAP("op": "Literal", "type": "regexp", "value": v))
    }

SubnetLiteral
  = v:ip6subnet !IdentifierRest {
      RETURN(MAP("op": "Literal", "type": "net", "value": v))
    }
  / v:subnet {
      RETURN(MAP("op": "Literal", "type": "net", "value": v))
    }

AddressLiteral
  = v:ip6addr !IdentifierRest {
      RETURN(MAP("op": "Literal", "type": "ip", "value": v))
    }
  / v:addr {
      RETURN(MAP("op": "Literal", "type": "ip", "value": v))
    }

FloatLiteral
  = v:sdouble {
      RETURN(MAP("op": "Literal", "type": "float64", "value": v))
    }

IntegerLiteral
  = v:sinteger {
      RETURN(MAP("op": "Literal", "type": "int64", "value": v))
    }

BooleanLiteral
  = "true"           { RETURN(MAP("op": "Literal", "type": "bool", "value": "true")) }
  / "false"          { RETURN(MAP("op": "Literal", "type": "bool", "value": "false")) }

NullLiteral
  = "null"           { RETURN(MAP("op": "Literal", "type": "null")) }

searchKeywords
  = andToken
  / orToken
  / inToken

procList
  = first:procChain rest:parallelChain* {
      VAR(fp) = MAP("op": "SequentialProc", "procs": first)
      if ISNOTNULL(rest) {
        RETURN(MAP("op": "ParallelProc", "procs": PREPEND(fp, rest)))
      } else {
        RETURN(fp)
      }
    }

parallelChain
  = __ ";" __ ch:procChain { RETURN(MAP("op": "SequentialProc", "procs": ch)) }

proc
  = simpleProc
  / groupByProc
  / "(" __ proc:procList __ ")" {
      RETURN(proc)
    }

groupByKeys
  = "by"i _ columns:FlexAssignments { RETURN(columns) }

// A FlexAssignment is like an Assignment but it can optionally omit the lhs,
// in which case, the semantic pass will infer a name from the rhs, e.g., for
// an expression like "count() by foo", the rhs is Field "foo" and the lhs is nil.
FlexAssignment
  = Assignment
  / expr:Expression { RETURN(MAP("op": "Assignment", "rhs": expr)) }

FlexAssignments
  = first:FlexAssignment rest:(__ "," __ expr:FlexAssignment { RETURN(expr) })* {
      RETURN(PREPEND(first, rest))
  }

everyDur
  = "every"i _ dur:duration { RETURN(dur) }

equalityToken
  = EqualityOperator / RelativeOperator

andToken = "and"i { RETURN(TEXT) }
orToken = "or"i { RETURN(TEXT) }
inToken = "in"i { RETURN(TEXT) }
notToken = "not"i { RETURN(TEXT) }

IdentifierName = IdentifierStart IdentifierRest* { RETURN(TEXT) }

IdentifierStart = [A-Za-z_$]
IdentifierRest = IdentifierStart / [0-9]

Identifier
  = IdentifierStart IdentifierRest* { RETURN(MAP("op": "Identifier", "name": TEXT)) }

RootField
  = "."? !(BooleanLiteral / NullLiteral) field:Identifier { RETURN(MAP("op": "BinaryExpr", "operator":".", "lhs":MAP("op":"RootRecord"), "rhs": field)) }
  / "." !(Identifier)  { RETURN(MAP("op": "RootRecord")) }

Lval = DerefExpression

DerefExpression
  = first:RootField rest:(Deref)* {
    RETURN(makeBinaryExprChain(first, rest))
   }

Deref
  = "[" expr:Expression "]" { RETURN(ARRAY("[", expr)) }
  / "." !(".") id:Identifier { RETURN(ARRAY(".", id)) }


// XXX this is here to support the function call stuff that used to live
// inside of FieldCall.  This will be replaced by a subset of the expression
// syntax in a subsequent PR (which is tricky due to mixin keyword search
// concantenation with expressions)
FunctionExpr
    = fn:FunctionName __ "(" args:ArgumentList ")" {
          RETURN(MAP("op": "FunctionCall", "function": fn, "args": args))
      }

fieldExpr = Lval

fieldExprList
  = first:fieldExpr rest:(__ "," __ fieldExpr)* {
      VAR(result) = ARRAY(first)

      FOREACH(ASSERT_ARRAY(rest), r) {
        APPEND(result, ASSERT_ARRAY(r)[3])
      }

      RETURN(result)
  }

ExprList
  = first:Expression rest:(__ "," __ Expression)* {
      VAR(result) = ARRAY(first)

      FOREACH(ASSERT_ARRAY(rest), r) {
        APPEND(result, ASSERT_ARRAY(r)[3])
      }

      RETURN(result)
    }

countOp
  = "count" { RETURN("count") }

ReducerOp
  = ReducerOps { RETURN(TEXT) }

ReducerOps
  = "sum"
  / "avg"
  / "stdev"
  / "var"
  / "entropy"
  / "min"
  / "max"
  / "first"
  / "last"
  / "countdistinct"

groupByProc
  = every:(everyDur _)? reducers:reducerList keys:(_ groupByKeys)? limit:procLimitArg? {
    if ISNOTNULL(OR(keys, every)) {
      if ISNOTNULL(keys) {
        keys = ASSERT_ARRAY(keys)[1]
      } else {
        keys = ARRAY()
      }

      if ISNOTNULL(every) {
        every = ASSERT_ARRAY(every)[0]
      }

      RETURN(MAP("op": "GroupByProc", "duration": every, "limit": limit, "keys": keys, "reducers": reducers))
    }
    RETURN(MAP("op": "GroupByProc", "reducers": reducers))
  }

ReducerAssignment
  = lval:Lval __ "=" reducer:reducer {
      RETURN(MAP("op": "Assignment", "lhs": lval, "rhs": reducer))
  }
  / reducer:reducer {
      RETURN(MAP("op": "Assignment", "rhs": reducer))
  }

reducer = countReducer / fieldReducer

countReducer
  = op:countOp __ "(" __ expr:fieldExpr?  __ ")" {
    VAR(r) = MAP("op": "Reducer", "operator": op)
    if ISNOTNULL(expr) {
      r["expr"] = expr
    }
    RETURN(r)
  }

fieldReducer
  = op:ReducerOp __ "(" __ expr:fieldExpr  __ ")" {
    VAR(r) = MAP("op": "Reducer", "operator": op)
    if ISNOTNULL(expr) {
      r["expr"] = expr
    }
    RETURN(r)
  }

reducerList
  = first:ReducerAssignment rest:(__ "," __ ReducerAssignment)* {
      VAR(result) = ARRAY(first)
      FOREACH(ASSERT_ARRAY(rest), r) {
        APPEND(result, ASSERT_ARRAY(r)[3])
      }
      RETURN(result)
    }

simpleProc
  = sort
  / top
  / cut
  / head
  / tail
  / filter
  / uniq
  / put
  / rename
  / fuse

sort
  = "sort"i args:sortArgs list:(_ l:ExprList { RETURN(l) })? {
    VAR(argm) = ASSERT_MAP(args)
    VAR(proc) = MAP("op": "SortProc", "fields": list, "sortdir": 1, "nullsfirst": false)
    if HAS(argm, "r") {
      proc["sortdir"] = -1
    }
    if HAS(argm, "nulls") {
      if (argm["nulls"] == "first") {
        proc["nullsfirst"] = true
      }
    }
    RETURN(proc)
  }

sortArgs = args:(_ a:sortArg{ RETURN(a) })* {
    return makeArgMap(args)
}

sortArg
  = "-r" { RETURN(MAP("name": "r", "value": NULL)) }
  / "-nulls" _ where:(("first" / "last") { RETURN(TEXT) } ) { RETURN(MAP("name": "nulls", "value": where)) }

top
  = "top"i limit:(_ n:unsignedInteger { RETURN(n)})? flush:(_ "-flush")? fields:(_ f:fieldExprList { RETURN(f) })? {
    VAR(proc) = MAP("op": "TopProc")
    if ISNOTNULL(limit) {
      proc["limit"] = limit
    }
    if ISNOTNULL(fields) {
      proc["fields"] = fields
    }
    if ISNOTNULL(flush) {
      proc["flush"] = true
    }
    RETURN(proc)
  }

procLimitArg
  = _ "with" _ "-limit" _ limit:unsignedInteger { RETURN(limit) }

cutArgs
  = args:(_ "-c" { RETURN(MAP("name": "c", "value": NULL)) })* {
    return makeArgMap(args)
  }

cut
  = "cut"i args:cutArgs _ columns:FlexAssignments {
    VAR(argm) = ASSERT_MAP(args)
    VAR(proc) = MAP("op": "CutProc", "fields": columns, "complement": false)
    if HAS(argm, "c") {
      proc["complement"] = true
    }
    RETURN(proc)
  }


head
  = "head"i _ count:unsignedInteger { RETURN(MAP("op": "HeadProc", "count": count)) }
  / "head"i { RETURN(MAP("op": "HeadProc", "count": 1)) }
tail
  = "tail"i _ count:unsignedInteger { RETURN(MAP("op": "TailProc", "count": count)) }
  / "tail"i { RETURN(MAP("op": "TailProc", "count": 1)) }

filter
  = "filter"i _ expr:searchExpr {
      RETURN(MAP("op": "FilterProc", "filter": expr))
    }
uniq
  = "uniq"i _ "-c" {
      RETURN(MAP("op": "UniqProc", "cflag": true))
    }
  / "uniq"i {
      RETURN(MAP("op": "UniqProc", "cflag": false))
    }

put
  = "put"i _ columns:FlexAssignments {
      RETURN(MAP("op": "PutProc", "clauses": columns))
    }

rename
  = "rename"i _ first:Assignment rest:(__ "," __ cl:Assignment { RETURN(cl) })* {
      RETURN(MAP("op": "RenameProc", "fields": PREPEND(first, rest)))
    }

fuse
  = "fuse"i {
      RETURN(MAP("op": "FuseProc"))
  }

Assignment
  = lhs:Lval __ "=" __ rhs:Expression { RETURN(MAP("lhs": lhs, "rhs": rhs)) }

Primary
  = StringLiteral
  / RegexpLiteral
  / SubnetLiteral
  / AddressLiteral
  / FloatLiteral
  / IntegerLiteral
  / BooleanLiteral
  / NullLiteral
  / "(" __ expr:Expression __ ")" { RETURN(expr) }

//
// Rules for parsing expressions.  Following standard practice, each
// level of operator precedence has a rule that references the next
// highest level of precedence.
//

Expression = ConditionalExpression

ConditionalExpression
  = condition:LogicalORExpression __ "?" __ thenClause:Expression __ ":" __ elseClause:Expression {
    RETURN(MAP("op": "ConditionalExpr", "condition": condition, "then": thenClause, "else": elseClause))
  }
  / LogicalORExpression

LogicalORExpression
  = first:LogicalANDExpression
    rest:(__ op:orToken __ expr:LogicalANDExpression{ RETURN(ARRAY(op, expr)) })* {
        RETURN(makeBinaryExprChain(first, rest))
    }

LogicalANDExpression
  = first:EqualityCompareExpression
    rest:(__ op:andToken __ expr:EqualityCompareExpression{ RETURN(ARRAY(op, expr)) })* {
        RETURN(makeBinaryExprChain(first, rest))
    }

EqualityCompareExpression
  = first:RelativeExpression
    rest:(__ comp:EqualityComparator __ expr:RelativeExpression{ RETURN(ARRAY(comp, expr)) })* {
        RETURN(makeBinaryExprChain(first, rest))
    }

EqualityOperator = ("=~" / "!~" / "=" / "!=") { RETURN(TEXT) }

EqualityComparator
  = EqualityOperator
  / "in" { RETURN(TEXT) }

RelativeExpression
  = first:AdditiveExpression
    rest:(__ op:RelativeOperator __ expr:AdditiveExpression{ RETURN(ARRAY(op, expr)) })* {
        RETURN(makeBinaryExprChain(first, rest))
    }

RelativeOperator = ("<=" / "<" / ">=" / ">") { RETURN(TEXT) }

AdditiveExpression
  = first:MultiplicativeExpression
    rest:(__ op:AdditiveOperator __ expr:MultiplicativeExpression{ RETURN(ARRAY(op, expr)) })* {
        RETURN(makeBinaryExprChain(first, rest))
    }

AdditiveOperator = ("+" / "-") { RETURN(TEXT) }

MultiplicativeExpression
  = first:NotExpression
    rest:(__ op:MultiplicativeOperator __ expr:NotExpression{ RETURN(ARRAY(op, expr)) })* {
        RETURN(makeBinaryExprChain(first, rest))
    }

MultiplicativeOperator = ("*" / "/") { RETURN(TEXT) }

NotExpression
  = "!" __ e:NotExpression {
        RETURN(MAP("op": "UnaryExpr", "operator": "!", "operand": e))
    }
  / CastExpression

CastExpression
  = e:FuncExpression typ:( ":" typ:PrimitiveType { RETURN(typ) }) {
    RETURN(MAP("op": "CastExpr", "expr": e, "type": typ))
  }
  / FuncExpression


PrimitiveType
  = (   "bytes" / "uint8" / "uint16" / "uint32" / "uint64"
      / "int8" / "int16" / "int32" / "int64"
      / "duration" / "time"
      / "float64"
      / "bool" / "bytes" / "string" / "bstring"
      / "ip" / "net"
      / "type" / "error" / "null" ) { RETURN(TEXT) }

FuncExpression
  = first:FunctionCall rest:(Deref)* {
    RETURN(makeBinaryExprChain(first, rest))
  }
  / DerefExpression
  / Primary

FunctionCall
  = fn:FunctionName __ "(" args:ArgumentList ")" {
        RETURN(MAP("op": "FunctionCall", "function": fn, "args": args))
    }

FunctionName
  = FunctionNameStart FunctionNameRest* { RETURN(TEXT) }

FunctionNameStart = [A-Za-z]
FunctionNameRest = FunctionNameStart / [.0-9]

ArgumentList
  = first:Expression rest:(__ "," __ e:Expression { RETURN(e) })* {
      RETURN(PREPEND(first, rest))
  }
  / __ { RETURN(ARRAY()) }

duration
  = seconds
  / minutes
  / hours
  / hours _ "and" _ minutes
  / days
  / weeks

sec_abbrev
  = "seconds"
  / "second"
  / "secs"
  / "sec"
  / "s"

min_abbrev
  = "minutes"
  / "minute"
  / "mins"
  / "min"
  / "m"

hour_abbrev
  = "hours"
  / "hrs"
  / "hr"
  / "h"
  / "hour"

day_abbrev = "days"/"day"/"d"
week_abbrev = "weeks"/"week"/"wks"/"wk"/"w"

seconds
  = "second" { RETURN(MAP("type": "Duration", "seconds": 1)) }
  / num:number __ sec_abbrev { RETURN(MAP("type": "Duration", "seconds": num)) }

minutes
  = "minute" { RETURN(MAP("type": "Duration", "seconds": 60)) }
  / num:number __ min_abbrev { RETURN(MAP("type": "Duration", "seconds": ASSERT_INT(num)*60)) }

hours
  = "hour" { RETURN(MAP("type": "Duration", "seconds": 3600)) }
  / num:number __ hour_abbrev { RETURN(MAP("type": "Duration", "seconds": ASSERT_INT(num)*3600)) }

days
  = "day" { RETURN(MAP("type": "Duration", "seconds": 3600*24)) }
  / num:number __ day_abbrev { RETURN(MAP("type": "Duration", "seconds": (ASSERT_INT(num)*3600*24))) }

weeks
  = "week" { RETURN(MAP("type": "Duration", "seconds": 3600*24*7)) }
  / num:number __ week_abbrev { RETURN(MAP("type": "Duration", "seconds": ASSERT_INT(num)*3600*24*7)) }

number = unsignedInteger


//XXX what about mac addrs?
addr
  = a:(unsignedInteger "." unsignedInteger "." unsignedInteger "." unsignedInteger) { RETURN(TEXT) }

// this matches a superset of legal syntax for ip6 addresses but the compiler
// will catch any errors when translating the filter
ip6addr
  = a:(h_prepend)+ b:ip6tail {
      RETURN(joinChars(a) + ASSERT_STRING(b))
    }
  / a:h16 b:(h_append)* "::" d:(h_prepend)* e:ip6tail {
      RETURN(ASSERT_STRING(a) + joinChars(b) + "::" + joinChars(d) + ASSERT_STRING(e))
    }
  / "::" a:(h_prepend)* b:ip6tail {
      RETURN("::" + joinChars(a) + ASSERT_STRING(b))
    }
  / a:h16 b:(h_append)* "::" {
      RETURN(ASSERT_STRING(a) + joinChars(b) + "::")
    }
  / "::" {
      RETURN("::")
    }

ip6tail
  = addr
  / h16

h_append = ":" v:h16 { RETURN(":" + ASSERT_STRING(v)) }
h_prepend = v:h16 ":" { RETURN(ASSERT_STRING(v) + ":") }

subnet
  = a:addr '/' m:unsignedInteger {
      RETURN(ASSERT_STRING(a) + "/" + TOSTRING(m));
    }

ip6subnet
  = a:ip6addr '/' m:unsignedInteger {
      RETURN(ASSERT_STRING(a) + "/" + ASSERT_STRING(m));
    }

unsignedInteger
  = s:suint { RETURN(parseInt(s)) }

suint
  = [0-9]+ { RETURN(TEXT) }

integer
  = s:sinteger { RETURN(parseInt(s)) }

sinteger
  = [+-]? suint { RETURN(TEXT) }

double
  = s:sdouble {
      RETURN(parseFloat(s))
  }

sdouble
  = "-"? doubleInteger+ "." doubleDigit+ exponentPart? {
      RETURN(TEXT)
    }
  / "-"? "." doubleDigit+ exponentPart? {
      RETURN(TEXT)
    }

doubleInteger
  = "0"
  / [1-9] [0-9]*

doubleDigit = [0-9]

exponentPart = "e"i sinteger

h16 = chars:hexdigit+ { RETURN(TEXT) }

hexdigit = [0-9a-fA-F]

searchWord
  = chars:searchWordPart+ { RETURN(joinChars(chars)) }

searchWordPart
  = "\\" s:(escapeSequence / searchEscape)  { RETURN(s) }
  / !([\x00-\x1F\x5C(),!><=DQUOTE|SQUOTE;:] / ws) . { RETURN(TEXT) }

quotedString
  = '"' v:doubleQuotedChar* '"' { RETURN(joinChars(v)) }
  / "'" v:singleQuotedChar* "'" { RETURN(joinChars(v)) }

doubleQuotedChar
  = !('"' / escapedChar) . { RETURN(TEXT) }
  / "\\" s:escapeSequence { RETURN(s) }

singleQuotedChar
  = !("'" / escapedChar) . { RETURN(TEXT) }
  / "\\" s:escapeSequence { RETURN(s) }

escapeSequence
  = "x" hexdigit hexdigit { RETURN("\\" + TEXT) }
  / singleCharEscape
  / unicodeEscape

singleCharEscape
  = "'"
  / '"'
  / "\\"
  / "b" { RETURN("\b") }
  / "f" { RETURN("\f") }
  / "n" { RETURN("\n") }
  / "r" { RETURN("\r") }
  / "t" { RETURN("\t") }
  / "v" { RETURN("\v") }

searchEscape
  = "=" { RETURN("=") }
  / "*" { RETURN("\\*") }

unicodeEscape
  = "u" chars:(hexdigit hexdigit hexdigit hexdigit) {
      RETURN(makeUnicodeChar(chars))
    }
  / "u" "{" chars:(hexdigit hexdigit? hexdigit? hexdigit? hexdigit? hexdigit?) "}" {
      RETURN(makeUnicodeChar(chars))
    }

reString
  = '/' v:reBody '/' { RETURN(v) }

reBody
  = ([^/\\]/"\\/")+ { RETURN(TEXT) }

escapedChar
  = [\x00-\x1f\\]

ws
  = "\t"
  / "\v"
  / "\f"
  / " "
  / "\u00A0"
  / "\uFEFF"

_ "whitespace" = ws+
__ = ws*

EOF = !.
