#define DQUOTE \x22
#define SQUOTE \x27

#ifdef GO
{
    package zql
    import "github.com/brimsec/zq/reglob"
}
#define RETURN(x) return x, nil
#define NULL nil
#define INIT_ASSIGN_VAR(x, var) x := var
#define ARRAY(...) []interface{}{__VA_ARGS__}
#define ARRAY_LEN(a) len(a.([]interface{}))
#define FOREACH(arr, var) for _, var := range arr
#define IF(stmt) if stmt
#define APPEND(arr, value) arr = append(arr, value)
#define PREPEND(value, arr) append([]interface{}{value}, (arr.([]interface{}))...)
#define TEXT string(c.text)
#define TOSTRING(s) fmt.Sprintf("%v", s)
#define ISNOTNULL(n) n != nil
#define ASSERT_ARRAY(a) a.([]interface{})
#define ASSERT_STRING(s) s.(string)
#define ASSERT_INT(i) i.(int)
#else
{
#include "parser-support.js"
}
#define RETURN(x) return x
#define NULL null
#define INIT_ASSIGN_VAR(x, var) let x = var
#define ARRAY(...) [__VA_ARGS__]
#define ARRAY_LEN(a) a.length
#define FOREACH(arr, var) for(let var of arr)
#define IF(stmt) if (stmt)
#define APPEND(arr, value) arr.push(value)
#define PREPEND(value, arr) [value, ...arr]
#define TEXT text()
#define ASSERT_INT(i) i
#define TOSTRING(s) s.toString()
#define ISNOTNULL(n) (n)
#define ASSERT_ARRAY(a) a
#define ASSERT_STRING(s) s
#endif


start = _? ast:query _? EOF { RETURN(ast) }

query
  = procs:procChain {
      INIT_ASSIGN_VAR(filt, makeFilterProc(makeMatchAll()))
      RETURN(makeSequentialProc(PREPEND(filt, procs)))
    }
  / s:search _? rest:chainedProc* {
      if(ARRAY_LEN(rest) == 0) {
          RETURN(s)
      } else {
          RETURN(makeSequentialProc(PREPEND(s, rest)))
      }
    }
  / s:search {
      RETURN(makeSequentialProc(ARRAY(s)))
    }

procChain
  = first:proc rest:chainedProc* {
      IF(ISNOTNULL(rest)) {
        RETURN(PREPEND(first, rest))
      } else {
        RETURN(ARRAY(first))
      }
    }

chainedProc = _? "|" _? p:proc { RETURN(p) }

search
  = expr:searchExpr {
      RETURN(makeFilterProc(expr))
    }

searchExpr
  = first:searchTerm rest:oredSearchTerm* {
      RETURN(makeOrChain(first, rest))
    }

oredSearchTerm = _ orToken _ t:searchTerm { RETURN(t) }

searchTerm
  = first:searchFactor rest:andedSearchTerm* {
      RETURN(makeAndChain(first, rest))
    }

andedSearchTerm = _ (andToken _)? f:searchFactor { RETURN(f) }

searchFactor
  = (notToken _ / "!" _?) e:searchExpr {
      RETURN(makeLogicalNot(e))
    }
  / !("-") s:searchPred { RETURN(s) }
  / "(" _? expr:searchExpr _? ")" { RETURN(expr) }

searchPred
  = "*" _? fieldComparator:equalityToken _? v:searchValue {
      RETURN(makeCompareAny(fieldComparator, false, v))
    }
  / "**" _? fieldComparator:equalityToken _? v:searchValue {
      RETURN(makeCompareAny(fieldComparator, true, v))
    }
  / f:fieldExpr _? fieldComparator:equalityToken _? v:searchValue {
      RETURN(makeCompareField(fieldComparator, f, v))
    }
  / v:searchValue _? inToken _? "*" {
      RETURN(makeCompareAny("in", false, v))
    }
  / v:searchValue _? inToken _? f:fieldReference {
      RETURN(makeCompareField("in", f, v))
    }
  / v:searchValue {
      IF(getValueType(v) == "string") {
        RETURN(makeOrChain(makeCompareAny("search", true, v), ARRAY(makeCompareAny("searchin", true, v))))
      }
      IF(getValueType(v) == "regexp") {
        IF(TEXT == "*") {
          RETURN(makeMatchAll())
        }
        RETURN(makeOrChain(makeCompareAny("eql", true, v), ARRAY(makeCompareAny("in", true, v))))
      }

      RETURN(makeOrChain(makeCompareAny("search", true, makeLiteral("string", TEXT)), ARRAY(makeCompareAny("searchin", true, makeLiteral("string", TEXT)), makeCompareAny("eql", true, v), makeCompareAny("in", true, v))))
    }

searchValue
  = StringLiteral
  / RegexpLiteral
  / PortLiteral
  / SubnetLiteral
  / AddressLiteral
  / FloatLiteral

  // Careful not to use IntegerLiteral unconditionally or it will consume
  // the beginning of something like 1234abcd which is a valid search word
  / i:IntegerLiteral !searchWord { RETURN(i) }

  / !(searchKeywords _) v:BooleanLiteral { RETURN(v) }
  / !(searchKeywords _) v:NullLiteral { RETURN(v) }
  / !((searchKeywords) _) v:searchWord {
      IF(reglob.IsGlobby(ASSERT_STRING(v))) {
         INIT_ASSIGN_VAR(re, reglob.Reglob(ASSERT_STRING(v)))
         RETURN(makeLiteral("regexp", re))
      }
      RETURN(makeLiteral("string", v))
    }

StringLiteral
  = v:quotedString {
      RETURN(makeLiteral("string", v))
    }

RegexpLiteral
  = v:reString {
      RETURN(makeLiteral("regexp", v))
    }

PortLiteral
  = v:port {
      RETURN(makeLiteral("port", v))
    }

SubnetLiteral
  = v:ip6subnet {
      RETURN(makeLiteral("net", v))
    }
  / v:subnet {
      RETURN(makeLiteral("net", v))
    }

AddressLiteral
  = v:ip6addr {
      RETURN(makeLiteral("ip", v))
    }
  / v:addr {
      RETURN(makeLiteral("ip", v))
    }

FloatLiteral
  = v:sdouble {
      RETURN(makeLiteral("float64", v))
    }

IntegerLiteral
  = v:sinteger {
      RETURN(makeLiteral("int64", v))
    }

BooleanLiteral
  = "true"           { RETURN(makeLiteral("bool", "true")) }
  / "false"          { RETURN(makeLiteral("bool", "false")) }

NullLiteral
  = "null"           { RETURN(makeLiteral("null", "")) }

searchKeywords
  = andToken
  / orToken
  / inToken

procList
  = first:procChain rest:parallelChain* {
      INIT_ASSIGN_VAR(fp, makeSequentialProc(first))
      IF(ISNOTNULL(rest)) {
        RETURN(makeParallelProc(PREPEND(fp, rest)))
      } else {
        RETURN(fp)
      }
    }

parallelChain
  = _? ";" _? ch:procChain { RETURN(makeSequentialProc(ch)) }

proc
  = simpleProc
  / reducerProc
  / "(" _? proc:procList _? ")" {
      RETURN(proc)
    }

groupBy
  = "by"i _ list:fieldExprList { RETURN(list) }

everyDur
  = "every"i _ dur:duration { RETURN(dur) }

equalityToken
  = "=" { RETURN("eql") }
  / "!=" { RETURN("neql") }
  / "<=" { RETURN("lte") }
  / ">=" { RETURN("gte") }
  / "<" { RETURN("lt") }
  / ">" { RETURN("gt") }

andToken = "and"i { RETURN(TEXT) }
orToken = "or"i { RETURN(TEXT) }
inToken = "in"i { RETURN(TEXT) }
notToken = "not"i { RETURN(TEXT) }

fieldName = fieldNameStart fieldNameRest* { RETURN(TEXT) }

fieldNameStart = [A-Za-z_$]
fieldNameRest = fieldNameStart / [0-9]

fieldReference
 = base:fieldName derefs:(
       "." field:fieldName    { RETURN(makeFieldCall("RecordFieldRead", NULL, field)) }
     / "[" index:suint "]" { RETURN(makeFieldCall("Index", NULL, index)) }
   )* {
     RETURN(chainFieldCalls(base, derefs))
   }

fieldExpr
  = op:fieldOp _? "(" _? field:fieldReference _? ")" {
      RETURN(makeFieldCall(op, field, NULL))
    }
  / fieldReference

fieldOp
  = "len"i { RETURN("Len") }

fieldExprList
  = first:fieldExpr rest:(_? "," _? fieldExpr)* {
      INIT_ASSIGN_VAR(result, ARRAY(first))

      FOREACH(ASSERT_ARRAY(rest), r) {
        APPEND(result, ASSERT_ARRAY(r)[3])
      }

      RETURN(result)
  }

fieldRefDotOnly
  = base:fieldName refs:("." field:fieldName { RETURN(makeFieldCall("RecordFieldRead", NULL, field)) } )* {
    RETURN(chainFieldCalls(base, refs))
  }

fieldRefDotOnlyList
  = first:fieldRefDotOnly rest:(_? "," _? ref:fieldRefDotOnly { RETURN(ref) })* {
  INIT_ASSIGN_VAR(result, ARRAY(first))
  FOREACH(ASSERT_ARRAY(rest), r) {
    APPEND(result, r)
  }
  RETURN(result)
  }

fieldNameList
  = first:fieldName rest:(_? "," _? fieldName)* {
      INIT_ASSIGN_VAR(result, ARRAY(first))
      FOREACH(ASSERT_ARRAY(rest), r) {
        APPEND(result, ASSERT_ARRAY(r)[3])
      }
      RETURN(result)
  }

countOp
  = "count"i { RETURN("Count") }

fieldReducerOp
  = "sum"i  { RETURN("Sum") }
  / "avg"i  { RETURN("Avg") }
  / "stdev"i { RETURN("Stdev") }
  / "sd"i   { RETURN("Stdev") }
  / "var"i  { RETURN("Var") }
  / "entropy"i { RETURN("Entropy") }
  / "min"i  { RETURN("Min") }
  / "max"i  { RETURN("Max") }
  / "first"i  { RETURN("First") }
  / "last"i  { RETURN("Last") }
  / "countdistinct"i { RETURN("CountDistinct") }

paddedFieldExpr = _? field:fieldExpr _? { RETURN(field) }

countReducer
  = op:countOp _? "(" field:paddedFieldExpr?  _? ")" {
    RETURN(makeReducer(op, "count", field))
  }

fieldReducer
  = op:fieldReducerOp _? "(" _? field:fieldExpr  _? ")" {
    RETURN(makeReducer(op, toLowerCase(op), field))
  }

reducerProc
  = every:(everyDur _)? reducers:reducerList keys:(_ groupBy)? limit:procLimitArg? {
    if ISNOTNULL(OR(keys, every)) {
      if ISNOTNULL(keys) {
        keys = ASSERT_ARRAY(keys)[1]
      } else {
        keys = ARRAY()
      }

      if ISNOTNULL(every) {
        every = ASSERT_ARRAY(every)[0]
      }

      RETURN(makeGroupByProc(every, limit, keys, reducers))
    }

    RETURN(makeReducerProc(reducers))
  }

asClause
  = "as"i _ v:fieldName { RETURN(v) }

reducerExpr
  = field:fieldExpr _? "=" _? f:reducer {
    RETURN(overrideReducerVar(f, field))
  }
  / f:reducer _ field:asClause {
    RETURN(overrideReducerVar(f, field))
  }
  / reducer

reducer
  = countReducer
  / fieldReducer

reducerList
  = first:reducerExpr rest:(_? "," _? reducerExpr)* {
      INIT_ASSIGN_VAR(result, ARRAY(first))
      FOREACH(ASSERT_ARRAY(rest), r) {
        APPEND(result, ASSERT_ARRAY(r)[3])
      }
      RETURN(result)
    }

simpleProc
  = sort
  / top
  / cut
  / head
  / tail
  / filter
  / uniq
  / put

sort
  = "sort"i args:sortArgs list:(_ l:fieldExprList { RETURN(l) })? {
    return makeSortProc(args, list)
  }

sortArgs = (_ a:sortArg { RETURN(a) })*

sortArg
  = "-limit" _ limit:suint { RETURN(makeArg("limit", limit)) }
  / "-r" { RETURN(makeArg("r", NULL)) }
  / "-nulls" _ where:(("first" / "last") { RETURN(TEXT) } ) { RETURN(makeArg("nulls", where)) }

top
  = "top"i limit:(_ n:unsignedInteger { RETURN(n)})? flush:(_ "-flush")? list:(_ f:fieldExprList { RETURN(f) })? {
    RETURN(makeTopProc(list, limit, flush))
  }

procLimitArg
  = _ "-limit" _ limit:unsignedInteger { RETURN(limit) }

cut
  = "cut"i _ list:fieldRefDotOnlyList { RETURN(makeCutProc(list)) }
head
  = "head"i _ count:unsignedInteger { RETURN(makeHeadProc(count)) }
  / "head"i { RETURN(makeHeadProc(1)) }
tail
  = "tail"i _ count:unsignedInteger { RETURN(makeTailProc(count)) }
  / "tail"i { RETURN(makeTailProc(1)) }

filter
  = "filter"i _ expr:searchExpr {
      RETURN(makeFilterProc(expr))
    }
uniq
  = "uniq"i _ "-c" {
      RETURN(makeUniqProc(true))
    }
  / "uniq"i {
      RETURN(makeUniqProc(false))
    }

put
  = "put"i _ f:fieldName __ "=" __ e:Expression {
      RETURN(makePutProc(f, e))
    }

PrimaryExpression
  = StringLiteral
  / RegexpLiteral
  / PortLiteral
  / SubnetLiteral
  / AddressLiteral
  / FloatLiteral
  / IntegerLiteral
  / BooleanLiteral
  / NullLiteral
  / FieldReference
  / "(" __ expr:Expression __ ")" { RETURN(expr) }

FieldReference
  = f:fieldName {
      RETURN(chainFieldCalls(f, ARRAY()))
    }

//
// Rules for parsing expressions.  Following standard practice, each
// level of operator precedence has a rule that references the next
// highest level of precedence.
//

Expression = ConditionalExpression

ConditionalExpression
  = condition:LogicalORExpression __ "?" __ thenClause:Expression __ ":" __ elseClause:Expression {
    RETURN(makeConditionalExpr(condition, thenClause, elseClause))
  }
  / LogicalORExpression

LogicalORExpression
  = first:LogicalANDExpression
    rest:(__ orToken __ LogicalANDExpression)* {
        RETURN(makeBinaryExprChain(first, rest))
    }

LogicalANDExpression
  = first:EqualityCompareExpression
    rest:(__ andToken __ EqualityCompareExpression)* {
        RETURN(makeBinaryExprChain(first, rest))
    }

EqualityCompareExpression
  = first:RelativeExpression
    rest:(__ EqualityOperator __ RelativeExpression)* {
        RETURN(makeBinaryExprChain(first, rest))
    }

EqualityOperator = ("=" / "!=") { RETURN(TEXT) }

RelativeExpression
  = first:AdditiveExpression
    rest:(__ RelativeOperator __ AdditiveExpression)* {
        RETURN(makeBinaryExprChain(first, rest))
    }

RelativeOperator = ("<=" / "<" / ">=" / ">") { RETURN(TEXT) }

AdditiveExpression
  = first:MultiplicativeExpression
    rest:(__ AdditiveOperator __ MultiplicativeExpression)* {
        RETURN(makeBinaryExprChain(first, rest))
    }

AdditiveOperator = ("+" / "-") { RETURN(TEXT) }

MultiplicativeExpression
  = first:NotExpression
    rest:(__ op:MultiplicativeOperator __ operand:NotExpression)* {
        RETURN(makeBinaryExprChain(first, rest))
    }

MultiplicativeOperator = ("*" / "/") { RETURN(TEXT) }

NotExpression
  = "!" __ e:NotExpression {
        RETURN(makeUnaryExpr("!", e))
    }
  / CallExpression


CallExpression
  = fn:FunctionName __ "(" args:ArgumentList ")" {
        RETURN(makeFunctionCall(fn, args))
    }
  / DereferenceExpression

FunctionName
  = FunctionNameStart FunctionNameRest* { RETURN(TEXT) }

FunctionNameStart = [A-Za-z]
FunctionNameRest = FunctionNameStart / [.0-9]

ArgumentList
  = first:Expression rest:(__ "," __ e:Expression { RETURN(e) })* {
      RETURN(PREPEND(first, rest))
  }
  / __ { RETURN(ARRAY()) }

DereferenceExpression
  = base:PrimaryExpression
    derefs:(
        __ "[" __ index:Expression __ "]"
      / __ "." __ (field:fieldName { RETURN(makeLiteral("string", TEXT)) })
    )* {
        RETURN(makeBinaryExprChain(base, derefs))
    }

duration
  = seconds
  / minutes
  / hours
  / hours _ "and" _ minutes
  / days
  / weeks

sec_abbrev
  = "seconds"
  / "second"
  / "secs"
  / "sec"
  / "s"

min_abbrev
  = "minutes"
  / "minute"
  / "mins"
  / "min"
  / "m"

hour_abbrev
  = "hours"
  / "hrs"
  / "hr"
  / "h"
  / "hour"

day_abbrev = "days"/"day"/"d"
week_abbrev = "weeks"/"week"/"wks"/"wk"/"w"

seconds
  = "second" { RETURN(makeDuration(1)) }
  / num:number _? sec_abbrev { RETURN(makeDuration(num)) }

minutes
  = "minute" { RETURN(makeDuration(60)) }
  / num:number _? min_abbrev { RETURN(makeDuration(ASSERT_INT(num)*60)) }

hours
  = "hour" { RETURN(makeDuration(3600)) }
  / num:number _? hour_abbrev { RETURN(makeDuration(ASSERT_INT(num)*3600)) }

days
  = "day" { RETURN(makeDuration(3600*24)) }
  / num:number _? day_abbrev { RETURN(makeDuration(ASSERT_INT(num)*3600*24)) }

weeks
  = num:number _? week_abbrev { RETURN(makeDuration(ASSERT_INT(num)*3600*24*7)) }

number = unsignedInteger


//XXX what about mac addrs?
addr
  = a:(unsignedInteger "." unsignedInteger "." unsignedInteger "." unsignedInteger) { RETURN(TEXT) }

port
  = ":" v:suint { RETURN(v) }

// this matches a superset of legal syntax for ip6 addresses but the compiler
// will catch any errors when translating the filter
ip6addr
  = a:(h_prepend)+ b:ip6tail {
      RETURN(joinChars(a) + ASSERT_STRING(b))
    }
  / a:h16 b:(h_append)* "::" d:(h_prepend)* e:ip6tail {
      RETURN(ASSERT_STRING(a) + joinChars(b) + "::" + joinChars(d) + ASSERT_STRING(e))
    }
  / "::" a:(h_prepend)* b:ip6tail {
      RETURN("::" + joinChars(a) + ASSERT_STRING(b))
    }
  / a:h16 b:(h_append)* "::" {
      RETURN(ASSERT_STRING(a) + joinChars(b) + "::")
    }
  / "::" {
      RETURN("::")
    }

ip6tail
  = addr
  / h16

h_append = ":" v:h16 { RETURN(":" + ASSERT_STRING(v)) }
h_prepend = v:h16 ":" { RETURN(ASSERT_STRING(v) + ":") }

sub_addr
  = addr
  / a:(unsignedInteger "." unsignedInteger "." unsignedInteger ) { RETURN(TEXT + ".0") }
  / a:(unsignedInteger "." unsignedInteger ) { RETURN(TEXT + ".0.0") }
  / a:unsignedInteger { RETURN(TEXT + ".0.0.0") }

subnet
  = a:sub_addr '/' m:unsignedInteger {
      RETURN(ASSERT_STRING(a) + "/" + TOSTRING(m));
    }

ip6subnet
  = a:ip6addr '/' m:unsignedInteger {
      RETURN(ASSERT_STRING(a) + "/" + ASSERT_STRING(m));
    }

unsignedInteger
  = s:suint { RETURN(parseInt(s)) }

suint
  = [0-9]+ { RETURN(TEXT) }

integer
  = s:sinteger { RETURN(parseInt(s)) }

sinteger
  = [+-]? suint { RETURN(TEXT) }

double
  = s:sdouble {
      RETURN(parseFloat(s))
  }

sdouble
  = "-"? doubleInteger+ "." doubleDigit+ exponentPart? {
      RETURN(TEXT)
    }
  / "-"? "." doubleDigit+ exponentPart? {
      RETURN(TEXT)
    }

doubleInteger
  = "0"
  / [1-9] [0-9]*

doubleDigit = [0-9]

exponentPart = "e"i sinteger

h16 = chars:hexdigit+ { RETURN(TEXT) }

hexdigit = [0-9a-fA-F]

searchWord
  = chars:searchWordPart+ { RETURN(joinChars(chars)) }

searchWordPart
  = "\\" s:(escapeSequence / searchEscape)  { RETURN(s) }
  / !([\x00-\x1F\x5C(),!><=DQUOTE|SQUOTE;] / ws) . { RETURN(TEXT) }

quotedString
  = '"' v:doubleQuotedChar* '"' { RETURN(joinChars(v)) }
  / "'" v:singleQuotedChar* "'" { RETURN(joinChars(v)) }

doubleQuotedChar
  = !('"' / escapedChar) . { RETURN(TEXT) }
  / "\\" s:escapeSequence { RETURN(s) }

singleQuotedChar
  = !("'" / escapedChar) . { RETURN(TEXT) }
  / "\\" s:escapeSequence { RETURN(s) }

escapeSequence
  = "x" hexdigit hexdigit { RETURN("\\" + TEXT) }
  / singleCharEscape
  / unicodeEscape

singleCharEscape
  = "'"
  / '"'
  / "\\"
  / "b" { RETURN("\b") }
  / "f" { RETURN("\f") }
  / "n" { RETURN("\n") }
  / "r" { RETURN("\r") }
  / "t" { RETURN("\t") }
  / "v" { RETURN("\v") }

searchEscape
  = "=" { RETURN("=") }
  / "*" { RETURN("\\*") }

unicodeEscape
  = "u" chars:(hexdigit hexdigit hexdigit hexdigit) {
      RETURN(makeUnicodeChar(chars))
    }
  / "u" "{" chars:(hexdigit hexdigit? hexdigit? hexdigit? hexdigit? hexdigit?) "}" {
      RETURN(makeUnicodeChar(chars))
    }

reString
  = '/' v:reBody '/' { RETURN(v) }

reBody
  = ([^/\\]/"\\/")+ { RETURN(TEXT) }

escapedChar
  = [\x00-\x1f\\]

ws
  = "\t"
  / "\v"
  / "\f"
  / " "
  / "\u00A0"
  / "\uFEFF"

_ "whitespace" = ws+
__ = ws*

EOF = !.
